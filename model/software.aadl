-- 
-- This model is part of the case study for the SHSS model
-- using AADL. These represent the software in the system.
-- 
package software
public

---------------------------
--  Process Definitions  --
---------------------------
	process MasterControlUnit
		features
		--Input Devices 
			photo_data_in: in data port;
			pin_data_in: in data port;
			read_write_data_in: in data port;
			detected_motion_event_in: in event port;
			detected_smoke_event_in: in event port;
			success_in: in event port;

			-- Output Devices
			display_data_out: out data port;
			write_read_data_out: out data port;
			take_photo_event_out: out event port;
			send_message_event_out: out event port;
			sound_alarm_event_out: out event port;
			flows 
				f0: flow path pin_data_in -> write_read_data_out;
				f1: flow path read_write_data_in -> display_data_out;
				f2: flow path read_write_data_in -> display_data_out;
				f3: flow path success_in -> take_photo_event_out;
				f4: flow path success_in -> sound_alarm_event_out;
				f5: flow path success_in -> send_message_event_out;
				f6: flow path photo_data_in -> write_read_data_out;

	end MasterControlUnit;

	-------------------------------
	--  Process Implementations  --
	-------------------------------
	process implementation MasterControlUnit.i
		subcomponents
			thr_login: thread PinEnteredThr.i;
			thr_system_armed: thread SystemArmedThr.i;
			thr_sound_alarm: thread SoundAlarmThr.i;
			thr_save_photo: thread SavePhotoThr.i;

		connections
		--PinEnteredThr connections
			t001: port pin_data_in -> thr_login.pin_input_data_in;
			t003: port read_write_data_in -> thr_login.data_in;
			t004: port thr_login.auth_results_data_out -> thr_system_armed.auth_results_data_in;
			t018: port thr_login.read_out -> write_read_data_out;

			--SystemArmedThr connections
			t006: port thr_system_armed.display_alarm_on_off -> display_data_out;
			t007: port thr_sound_alarm.sendMessage_event_out -> send_message_event_out;
			t008: port detected_motion_event_in -> thr_sound_alarm.alarm_triggered_in;
			t009: port thr_system_armed.sound_alarm_event_out -> thr_sound_alarm.alarm_triggered_in;

			--SoundAlarmThr
			t010: port thr_sound_alarm.display_warning_message -> display_data_out;
			t011: port thr_sound_alarm.alarm_triggered_out -> sound_alarm_event_out;
			t013: port detected_smoke_event_in -> thr_sound_alarm.alarm_triggered_in;
			t017: port thr_sound_alarm.take_photo_event_out -> take_photo_event_out;			
			t019: port success_in -> thr_sound_alarm.alarm_triggered_in;

			--SavePhotoThr
			t015: port photo_data_in -> thr_save_photo.photo_data_in;
			t016: port thr_save_photo.write_photo_out -> write_read_data_out;			
			
		flows
			--f0: flow path pin_data_in -> t001;
			f0: flow path pin_data_in -> t001 -> thr_login.f0 -> t018 -> write_read_data_out;
			f1: flow path read_write_data_in -> t003 -> thr_login.f1 -> t004 ->  thr_system_armed.f0 -> t006 -> display_data_out;
			f2: flow path read_write_data_in -> t003 -> thr_login.f1 -> t004 ->  thr_system_armed.f1 -> t009 -> thr_sound_alarm.f0 -> t010 -> display_data_out;
			f3: flow path success_in -> t019 ->thr_sound_alarm.f1 -> t017 -> take_photo_event_out;
			f4: flow path success_in -> t019 ->thr_sound_alarm.f2 -> t011 -> sound_alarm_event_out;
			f5: flow path success_in -> t019 -> thr_sound_alarm.f3 -> t007 -> send_message_event_out;
			f6: flow path photo_data_in -> t015 -> thr_save_photo.f0 -> t016 ->write_read_data_out;



	end MasterControlUnit.i;

	-------------------------------
	--  MC Threads  Definitions  --
	-------------------------------
	thread PinEnteredThr
		features
			pin_input_data_in: in data port;
			read_out: out data port;
			data_in: in data port;
			auth_results_data_out: out data port;
		flows
			f0: flow path pin_input_data_in -> read_out;
			f1: flow path data_in -> auth_results_data_out;
		properties
			Dispatch_Protocol => Periodic;
			Compute_Execution_Time => 3 ms .. 5 ms;
			Period => 100 ms;
			--Priority => 12;

	end PinEnteredThr;

	thread SystemArmedThr
		features
			display_alarm_on_off: out data port;
			auth_results_data_in: in data port;
			sound_alarm_event_out: out event port;
		flows
			f0: flow path auth_results_data_in -> display_alarm_on_off;
			f1: flow path auth_results_data_in -> sound_alarm_event_out;
		properties
			Dispatch_Protocol => Periodic;
			Compute_Execution_Time => 3 ms .. 5 ms;
			Period => 50 ms;
			--Priority => 2;

	end SystemArmedThr;

	thread SoundAlarmThr
		features
			display_warning_message: out data port;
			alarm_triggered_in: in event port;
			alarm_triggered_out: out event port;
			take_photo_event_out: out event port;
			sendMessage_event_out: out event port;
		flows
			f0: flow path alarm_triggered_in -> display_warning_message;
			f1: flow path alarm_triggered_in -> take_photo_event_out;
			f2: flow path alarm_triggered_in -> alarm_triggered_out;
			f3: flow path alarm_triggered_in -> sendMessage_event_out;
		properties
			Dispatch_Protocol => Periodic;
			Compute_Execution_Time => 3 ms .. 5 ms;
			Period => 50 ms;
			--Priority => 1;

	end SoundAlarmThr;

	thread SavePhotoThr
		features
			photo_data_in: in data port;
			write_photo_out: out data port;
		flows
			f0: flow path photo_data_in -> write_photo_out;
		properties
			Dispatch_Protocol => Periodic;
			Compute_Execution_Time => 3 ms .. 5 ms;
			Period => 100 ms;
			--Priority => 2;

	end SavePhotoThr;

	-----------------------------------
	--  MC Threads  Implementations  --
	-----------------------------------
	thread implementation PinEnteredThr.i
	end PinEnteredThr.i;

	thread implementation SystemArmedThr.i
	end SystemArmedThr.i;

	thread implementation SoundAlarmThr.i
	end SoundAlarmThr.i;

	thread implementation SavePhotoThr.i
	end SavePhotoThr.i;

end software;